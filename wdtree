#!/usr/bin/env perl
use v5.14;
use Pod::Usage;
use Getopt::Long;
use Catmandu -all;
use HTTP::Tiny;
use RDF::Trine::Iterator::JSONHandler;
use Graph;

my %opt;
my ($input);

# get options
{
    Getopt::Long::Configure('bundling');
    GetOptions(\%opt, 
        'language|g|l=s',
        'root|Q=s',
        'property|prop|p|P=s',
        'help|h|?',
        'from|f=s',
        'size|s=s',
        'field=s',
        'dry',
    ) or pod2usage(2);
    pod2usage(1) if $opt{help};

    # root item as first argument
    if ( @ARGV and $ARGV[0] =~ /^Q?(\d+)(\.[a-z]+)?$/i) {
        $opt{root} = "Q$1";
        shift @ARGV;
    }

    # input file as argument 
    if ( @ARGV ) {
        $opt{from} = shift @ARGV;
    }

    # default values
    $opt{language} ||= 'en';
    $opt{property} ||= 'P279';  # subclass of
    $opt{size}     ||= 'P31';
}

# validate and normalize options
{
    $opt{root} or pod2usage("missing root item");
    $opt{root} =~ /^Q?(\d+)$/i or pod2usage("invalid root item"); 
    $opt{root} = "Q$1";

    foreach (qw(property size field)) {
        if (defined $opt{$_}) {
            $opt{$_} =~ /^P?(\d+)$/i or pod2usage("invalid $_"); 
            $opt{$_} = "P$1";
        }
    }
}


my $root = $opt{root};

if ($opt{from}) {
    open($input, '<', $opt{from}) or die "Failed to open $opt{from}\n";
} else { 

    my $property = $opt{property};
    my $size     = $opt{size};

    my $vars = "?item ?itemLabel ?broader ?parents ?size ?sites";
	my $sparql = <<SPARQL;
{
    { 
		SELECT ?item (count(distinct ?parent)-1 as ?parents) {
			?item wdt:$property* wd:$root
			OPTIONAL { ?item wdt:$property ?parent }
		} GROUP BY ?item 
	}
  	{ 
		SELECT ?item (count(distinct ?element) as ?size) {
			?item wdt:$property* wd:$root
			OPTIONAL { ?element wdt:$size ?item }
		} GROUP BY ?item 
    }
    { 
		SELECT ?item (count(distinct ?site) as ?sites) {
			?item wdt:$property* wd:$root
			OPTIONAL { ?site schema:about ?item }
		} GROUP BY ?item 
	}
  	OPTIONAL { ?item wdt:$property ?broader }
SPARQL

    if ($opt{field}) {
        $sparql .= "OPTIONAL { ?item wdt:$opt{field} ?field }";
        $vars .= " ?field";
    }

	$sparql = $sparql . <<SPARQL;
  	SERVICE wikibase:label {
		bd:serviceParam wikibase:language "$opt{language}" .
	}
}
SPARQL
    $sparql = "SELECT $vars $sparql";
    $sparql =~ s/\n/ /mg;
    my $cmd = "wdq -i -g$opt{language} -lid '$sparql'";
    #my $cmd = "wdq -i '$sparql'";

    open($input, "$cmd |") ## no critic
        or die "wdq execution failed!\n";
}

sub wikidata_query {
    my $sparql = shift;
    say($sparql) && exit if $opt{dry};
    
    my $query = { query => $sparql, format => 'json' };
    my $http = HTTP::Tiny->new(timeout => 30);
    my $url = 'https://query.wikidata.org/bigdata/namespace/wdq/sparql?'
            . '?' . $http->www_form_urlencode($query);
    my $res = $http->get($url);

    die "SPARQL query failed\n" unless $res->{success};
    my $rdf = RDF::Trine::Iterator::JSONHandler->new->parse($res->{content});

    while ( my $row = $rdf->next ) {
        ...
    }
}

# parse SPARQL query result

my %items;
my $g = Graph->new;

importer( 'JSON', fh => $input )->each(sub{
    my $id = $_[0]->{item};
	$_[0]->{label} = delete $_[0]->{itemLabel};

    $items{$id} = $_[0];
    warn "$id is no item!\n" if $id !~ /^Q\d+$/;

    if ( $_[0]->{broader} ) {
        if ( $id eq $_[0]->{broader} ) {
            warn "$id is subclass of itself!\n"
        } else {
            $g->add_edge( $_[0]->{item}, $_[0]->{broader} );
        }
    }
});

unless ($items{$root}) {
    die "Wikidata item $root not found!\n";
}

# emit classification in CSV format

use Text::CSV;
use autodie;

my $csv = Text::CSV->new({ binary => 1, sep_char => ',', quote_char => '"', eol => "\n" });
open my $csvfile, '>', "$root.csv";
binmode $csvfile, ':encoding(UTF-8)';

my @fields = qw(level item label parents size sites);
push @fields, 'field' if $opt{field}; 

sub make_tree {
    my ($node, $level) = @_;
    
    my $item = $items{$node};
    my $recurse = 1;

    if ( defined $item->{level} ) {
        $item->{level} = '=' x $level;
        $recurse = 0;
    } else {
        $item->{level} = '-' x $level;
    }

    # TODO: qualifier
    
    $csv->print( $csvfile, [ map { $item->{$_} } @fields ] );

    if ( $recurse ) {
        my @children = map { $_->[0] } $g->edges_to($node);
        foreach (sort { substr($a,1) cmp substr($b,1) } @children) {
            make_tree($_, $level+1);
        }
    }
}

$csv->print( $csvfile, \@fields );
make_tree($root);

__END__

=head1 NAME

wdtree - extract tree structures from Wikidata

=head1 USAGE

wdtree [options] [ [Qitem] | file.json ]

For instance C<wdtree Q6423319> extracts a classification of Knowledge
Organization Systems (Q6423319) and its subclasses (P279).

=cut
